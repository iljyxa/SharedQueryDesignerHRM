
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТекстЗапросаПредставления = Параметры.ТекстЗапросаПредставления;	
	ОбновитьИсполняемыйТекстЗапроса = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаТекстЗапросаИсполняемый
		И ОбновитьИсполняемыйТекстЗапроса Тогда
		
		Попытка
			ТекстЗапросаИсполняемый = ИсполняемыйТекстЗапросаПредставления(ТекстЗапросаПредставления);
		Исключение
			ТекстЗапросаИсполняемый =
				НСтр("ru = 'Произошла ошибка при формировании текста запроса. Возможно, в представлении допущена ошибка:'")
				+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						
		КонецПопытки;
		
		ОбновитьИсполняемыйТекстЗапроса = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстЗапросаПредставленияПриИзменении(Элемент)
	ОбновитьИсполняемыйТекстЗапроса = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура СкопироватьВБуферОбмена(Команда)
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТекстЗапросаИсполняемый Тогда
		ДанныеДляПомещенияВБуфер = ТекстЗапросаИсполняемый;
	Иначе
		ДанныеДляПомещенияВБуфер = ТекстЗапросаПредставления;	
	КонецЕсли;
	
	ЭлементБуфераОбмена = Новый ЭлементБуфераОбмена(СтандартныйФорматДанныхБуфераОбмена.Текст, ДанныеДляПомещенияВБуфер);
	Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ЭлементБуфераОбмена);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИсполняемыйТекстЗапросаПредставления(Знач ТекстЗапросаПредставления)
	
	Если ПустаяСтрока(ТекстЗапросаПредставления) Тогда
		Возврат "";
	КонецЕсли;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	МодульОбщегоНазначения = ОбработкаОбъект.МодульОбщегоНазначения();
	МодульЗарплатаКадрыОбщиеНаборыДанных = МодульОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыОбщиеНаборыДанных");
	МодульЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьЗапросыКПредставлениямВиртуальныхТаблиц(ТекстЗапросаПредставления);
	
	Возврат ТекстЗапросаПредставления;
		
КонецФункции

#КонецОбласти
